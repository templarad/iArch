grammar jp.ac.kyushu.iarch.archdsl.ArchDSL with org.eclipse.xtext.common.Terminals

generate archDSL "http://www.ac.jp/kyushu/iarch/archdsl/ArchDSL"

Model:
    (interfaces+=Interface |
    u_interfaces+=UncertainInterface|
    behaviors+=Behavior |
    u_behaviors+=UncertainBehavior|
    connectors+=Connector)*;

UncertainInterface:
    "uncertain component" name=ID ("extends" superInterface=[Interface])? "{"
    (altmethods+=AltMethod |
    optmethods+=OptMethod)*
    "}";

Interface:
    "interface component" name=ID "{"
    methods+=Method*
    "}";

Method:
    type=ID name=ID "(" (param+=Param ("," param+=Param)*)? ");";

AltMethod:
    "{"
    type+=ID name=ID "(" (param+=Param ("," param+=Param)*)? ")" ("," type+=ID a_name+=ID "(" (param+=Param (","
    param+=Param)*)? ")")*
    "};";

OptMethod:
    "["
    type=ID name=ID "(" (param+=Param ("," param+=Param)*)? ");"
    "]";

Param:
    type=ID name=ID;

Connector:
    "interface connector" name=ID"{"
        behaviors+=Behavior*
    "}"
;

UncertainConnector:
    "uncertain connector" name=ID ("extends" superInterface=[Interface])? "{"
        u_behaviors+=UncertainBehavior*
    "}"
;

Behavior:
    interface=[Interface] "=" "(" (call+=[Method|FQN] ("->" call+=[Method|FQN])* "->" end=[Interface])? ");";

SuperMethod:
    Method | OptMethod | AltMethod;

UncertainBehavior:
    "uc" name=ID ("extends" superInterface=[Interface])? "=" "("
    (call+=SuperCall ("->" call+=SuperCall)* "->" end=[Interface])?
    ");";

CertainCall:
    name=[SuperMethod|FQN];

OptCall:
    "[" name=[SuperMethod|FQN] "]";

AltCall:
    "{" (opt?="null" ",")? name=[SuperMethod|FQN] ("," (a_name+=[SuperMethod|FQN] | (opt?="null")?))* "}";

SuperCall:
    CertainCall | OptCall | AltCall;

FQN:
    ID ("." ID)*;